//
// Kino/Bokeh - Depth of field effect
//
// Copyright (C) 2016 Unity Technologies
// Copyright (C) 2015 Keijiro Takahashi
//
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
//
// The above copyright notice and this permission notice shall be included in
// all copies or substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
// THE SOFTWARE.
//



//#include "Common.cginc"
#include "DiskKernel.tfx"


Texture2D texture2d : PREVIOUS;

SamplerState linearSampler
{
    Filter = MIN_MAG_MIP_LINEAR;
    AddressU = CLAMP;
    AddressV = CLAMP;
};

struct psInput
{
	float4 p : SV_Position;
	float2 uv : TEXCOORD0;
};

static const float PI = 3.14159265f;

// Camera parameters
float RcpAspect;
float MaxCoC = 0.0100;

float2 MainTex_TexelSize;

// Fragment shader: Bokeh filter with disk-shaped kernels
half4 frag_Blur(psInput i) : SV_Target
{
    half4 samp0 = texture2d.Sample(linearSampler,  i.uv);

    half4 bgAcc = 0; // Background: far field bokeh
    half4 fgAcc = 0; // Foreground: near field bokeh

    for (uint si = 0; si < kSampleCount; si++)
    {
        float2 disp = kDiskKernel[si] * MaxCoC;
        float dist = length(disp);

        float2 duv = float2(disp.x * RcpAspect, disp.y);
        half4 samp = texture2d.Sample(linearSampler, i.uv + duv);

        // BG: Compare CoC of the current sample and the center sample
        // and select smaller one.
        half bgCoC = max(min(samp0.a, samp.a), 0);

        // Compare the CoC to the sample distance.
        // Add a small margin to smooth out.
        const half margin = MainTex_TexelSize.y * 2;
        half bgWeight = saturate((bgCoC   - dist + margin) / margin);
        half fgWeight = saturate((-samp.a - dist + margin) / margin);

        // Cut influence from focused areas because they're darkened by CoC
        // premultiplying. This is only needed for near field.
        fgWeight *= step(MainTex_TexelSize.y, -samp.a);

        // Accumulation
        bgAcc += half4(samp.rgb, 1) * bgWeight;
        fgAcc += half4(samp.rgb, 1) * fgWeight;
    }

    // Get the weighted average.
    bgAcc.rgb /= bgAcc.a + (bgAcc.a == 0); // zero-div guard
    fgAcc.rgb /= fgAcc.a + (fgAcc.a == 0);

    // BG: Calculate the alpha value only based on the center CoC.
    // This is a rather aggressive approximation but provides stable results.
    bgAcc.a = smoothstep(MainTex_TexelSize.y, MainTex_TexelSize.y * 2, samp0.a);

    // FG: Normalize the total of the weights.
    fgAcc.a *= PI / kSampleCount;

    // Alpha premultiplying
    half3 rgb = 0;
    rgb = lerp(rgb, bgAcc.rgb, saturate(bgAcc.a));
    rgb = lerp(rgb, fgAcc.rgb, saturate(fgAcc.a));

    // Combined alpha value
    half alpha = (1 - saturate(bgAcc.a)) * (1 - saturate(fgAcc.a));

	return half4(rgb, alpha);
}

technique10 Process
{
	pass P0<string format="R32G32B32A32_Float";>
	{
		SetPixelShader(CompileShader(ps_4_0,frag_Blur()));
	}

}
